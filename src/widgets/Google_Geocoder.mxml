<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:fx="http://ns.adobe.com/mxml/2009"
		   xmlns:mx="library://ns.adobe.com/flex/mx"
		   creationComplete="creationCompleteHandler()">
	
	<fx:Script>
		<![CDATA[
			import com.esri.ags.Graphic;
			import com.esri.ags.SpatialReference;
			import com.esri.ags.geometry.MapPoint;
			import com.esri.ags.geometry.WebMercatorMapPoint;
			import com.esri.ags.symbols.SimpleMarkerSymbol;
			
			import components.Google_Geocoder_PopUp;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.http.HTTPService;
			
			private var popup:Google_Geocoder_PopUp;
			private var service:HTTPService = new HTTPService();
			
			private function creationCompleteHandler():void
			{
				parentApplication.gl.clear();
				parentApplication.gll.clear();
				parentApplication.map.removeLayer(parentApplication.kmlLayer);
				
				popup = PopUpManager.createPopUp(this, Google_Geocoder_PopUp) as Google_Geocoder_PopUp;
				popup["locate"].addEventListener(MouseEvent.CLICK, httpService);
				popup.addEventListener(Event.CLOSE, closeModule);
				popup["address"].setFocus();
			}
			
			private function httpService(event:MouseEvent):void
			{
				service.url = "http://www.geospatialweb.ca/rest/api/geocoder/google_geocoder.php?address=" + popup.address.text + "&city=" + popup.municipality.text + "&prov=" + popup.province.selectedItem.prov;
				service.method = "GET";
				service.resultFormat = "e4x";
				service.addEventListener("result", httpResult);
				service.addEventListener("fault", httpFault);
				service.showBusyCursor = true;
				service.send();
			}
			
			private function httpFault(event:FaultEvent):void
			{
				var fault:String = event.message.toString();
				
				Alert.show(fault);
			}
			
			private function httpResult(event:ResultEvent):void
			{
				var result:XML = new XML(event.result.toString());
				var lat:Number = parseFloat(result..location.lat.toString());
				var lng:Number = parseFloat(result..location.lng.toString());
				var latlng:MapPoint = new MapPoint(lng, lat, new SpatialReference(4326));
				var xy:MapPoint = new WebMercatorMapPoint(lng, lat);
					
				addMarker(xy);
				displayResult(latlng, xy);
			} 
			
			private function addMarker(geometry:MapPoint):void
			{
				parentApplication.map.level = 15;
				parentApplication.map.centerAt(geometry);
				parentApplication.gl.add(new Graphic(geometry, new SimpleMarkerSymbol(SimpleMarkerSymbol.STYLE_DIAMOND, 10, 0xFF0000, 0.75)));
			}
			
			private function displayResult(latlng:MapPoint, xy:MapPoint):void
			{
				popup.X_Geog.text = latlng.x.toString() + " decimal degrees";
				popup.Y_Geog.text = latlng.y.toString() + " decimal degrees";
				popup.X_Merc.text = Math.round(xy.x).toString() + " metres";
				popup.Y_Merc.text = Math.round(xy.y).toString() + " metres";
			}
			
			private function closeModule(event:CloseEvent):void
			{
				PopUpManager.removePopUp(popup);
				popup["locate"].removeEventListener(MouseEvent.CLICK, httpService);
				popup.removeEventListener(Event.CLOSE, closeModule);
				
				service.removeEventListener("result", httpResult);
				service.removeEventListener("fault", httpFault);
				
				parentApplication.gl.clear();
				parentApplication.tbb.selectedIndex = "-1";
				parentApplication.ml.url = "";
			}
		]]>
	</fx:Script>

</mx:Module>